plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "legacy-looming" version "1.3-SNAPSHOT" apply false
    // id "ploceus" version "1.3-SNAPSHOT" apply false
    id "io.github.juuxel.loom-vineflower" version "1.11.0"
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
    subprojects.forEach {
        include(project(path: it.path, configuration: 'namedElements'))
    }
}

allprojects {
    apply plugin: "fabric-loom"
    apply plugin: "io.github.juuxel.loom-vineflower"

    version = "$mod_version+$project.target_version"
    group = maven_group

    base {
        //noinspection GrDeprecatedAPIUsage
        archivesBaseName = archives_base_name
    }

    dependencies {
        modImplementation "net.fabricmc:fabric-loader:$loader_version"
    }

    processResources {
        inputs.property "version", project.version
        inputs.property "target_version", target_version

        filesMatching("fabric.mod.json") {
            expand "version": project.version, "target_version": target_version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release.set(8)
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    vineflower {
        toolVersion = "1.9.2"
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}
